+package org.eolang.mp
+alias org.eolang.txt.sprintf
+alias org.eolang.io.stdout
+alias org.eolang.mp.multimap
+junit

[] > pairs-to-hash-test
  multimap * > mp
  * (* "key" 2) (* 2 3) (* 3 4) > arr
  mp.pairs-to-hash arr > tarr
  seq > @
    stdout
      sprintf
        "tarr size = %d\nvalues: %d %d %d\n"
        tarr.length
        tarr.at 0
        tarr.at 1
        tarr.at 2
    eq.
      tarr.at 2
      3

[] > concat-all-arrays-test
  multimap * > mp
  * (* 1 2 3) (* 4 5 6 7) (* 8) (*) (* 9) (*) > arr
  mp.concat-all-arrays arr > res
  and. > @
    eq.
      res.length
      9
    eq.
      res.at 3
      4

[] > with-do-not-crashed
  multimap * > mp
  (mp.with 1 2).with 3 4 > mp2
  seq > @
    stdout
      sprintf
        "%d\n"
        mp2.length
    TRUE

# Speed test
[] > with-many-objects
  multimap * > mp
  ((((((((mp.with 1 "a").with 2 "b").with 3 "c").with 4 "d").with 5 "e").with 6 "f").with 7 "g").with 8 "h") > mp2
  eq. > @
    mp2.length
    8

[] > find-in-list-works
  multimap * > mp!
  * (* 1 2) (* "abc" "bca") (* "a" 42) > list!
  mp.find-in-list "abc" list > res!
  mp.find-in-list "c" list > res2!
  and. > @
    eq.
      res.length
      1
    eq.
      res2.length
      0

[] > find-do-not-crashed
  multimap * > mp!
  mp.find 1 > res!
  eq. > @
    res.length
    0

[] > find-works
  multimap (* (*) (* (* 4 "a")) (* (* 2 "c") (* 8 "c"))) > mp!
  mp.find 2 > r2!
  mp.find 3 > r3!
  mp.find 4 > r4!
  mp.find 8 > r8!
  and. > @
    eq.
      r2.at 0
      "c"
    eq.
      r3.length
      0
    eq.
      r4.at 0
      "a"
    eq.
      r8.at 0
      "c"

[] > find-and-with
  multimap * > fmp!
  ((((fmp.with 1 "a").with "abc" "cba").with 4 5).with 3 5) > mp!
  mp.find 1 > r1!
  mp.find 2 > r2!
  mp.find 3 > r3!
  mp.find 4 > r4!
  mp.find "abc" > rabc!
  and. > @
    eq.
      r1.at 0
      "a"
    eq.
      r2.length
      0
    eq.
      r3.at 0
      5
    eq.
      r4.at 0
      5
    eq.
      rabc.at 0
      "cba"

[] > map-of-maps
  multimap * > init-mp!
  (init-mp.with 1 2).with 3 4 > mp1!
  (init-mp.with "a" "b").with "c" "d" > mp2!
  init-mp.with TRUE FALSE > mp3!
  ((init-mp.with 0 mp1).with 1 mp2).with 2 mp3 > mp-res!
  and. > @
    eq.
      (mp-res.find -1).length
      0
    eq.
      (((mp-res.find 1).at 0).find "c").at 0
      "d"
