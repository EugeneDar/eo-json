+package org.eolang.jsn
+alias org.eolang.jsn.json
+alias org.eolang.jsn.json-array
+alias org.eolang.jsn.json-int
+alias org.eolang.jsn.json-string
+alias org.eolang.hamcrest.assert-that
+alias org.eolang.collections.list
+junit

[] > hardcode-json-array-as-string
  json-int 10 > a!
  json-string "abc" > b!
  json-array (* a b) > ja!
  json-array (*) > ja2!
  assert-that > @
    * (ja.as-string) (ja2.as-string)
    $.equal-to
      list
        * "[10, \"abc\", ]" "[]"

[] > json-array-creating
  json.of-array > ja!
  assert-that > @
    ja.as-string
    $.equal-to "[]"

[] > json-array-with
  with. > ja!
    with.
      json.of-array
      json.of-int 12
    json.of-int 13
  assert-that > @
    ja.as-string
    $.equal-to "[12, 13, ]"

[] > json-array-at
  with. > ja!
    with.
      json.of-array
      json.of-int 12
    json.of-string "abacaba"
  assert-that > @
    * ((ja.at 0).as-string) ((ja.at 1).as-string) (ja.as-string)
    $.equal-to
      list
        * "12" "\"abacaba\"" "[12, \"abacaba\", ]"
